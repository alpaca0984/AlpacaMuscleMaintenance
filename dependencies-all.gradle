// References:
//  - https://medium.com/tapdevs/part-1-clean-gradle-files-gradle-dependencies-group-bd9bb33c81cf
//  - https://github.com/janshair/Android-MVVM/blob/develop/dependencies-all.gradle

def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

// mockito - https://site.mockito.org/
def mockito_version = '3.7.7'

ext {
    core = dependencyGroup {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        // Timber - https://github.com/JakeWharton/timber
        implementation 'com.jakewharton.timber:timber:4.7.1'
    }

    testing = dependencyGroup {
        // unit tests - https://developer.android.com/training/testing/unit-testing/local-unit-tests
        def junit_version = '4.13.1'
        testImplementation "junit:junit:$junit_version"

        // mockito
        testImplementation "org.mockito:mockito-core:$mockito_version"
    }

    androidTesting = dependencyGroup {
        // AndroidX core library - https://developer.android.com/training/testing/set-up-project
        def androidx_version = '1.3.0'
        androidTestImplementation "androidx.test:core:$androidx_version"
        androidTestImplementation "androidx.test:runner:$androidx_version"
        androidTestImplementation "androidx.test:rules:$androidx_version"

        // Espresso - https://developer.android.com/training/testing/espresso
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

        // mockito
        androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    }

    lifecycle = dependencyGroup {
        // Lifecycle - https://developer.android.com/jetpack/androidx/releases/lifecycle
        def lifecycle_version = "2.2.0"
        implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
        implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
        kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    }

    coroutines = dependencyGroup {
        // kotlinx.coroutines - https://github.com/Kotlin/kotlinx.coroutines
        def coroutines_version = '1.4.2'
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    }

    dagger2 = dependencyGroup {
        // Dagger 2 - https://github.com/google/dagger
        def dagger_version = '2.31.1'
        implementation "com.google.dagger:dagger:$dagger_version"
        kapt "com.google.dagger:dagger-compiler:$dagger_version"
        implementation "com.google.dagger:dagger-android:$dagger_version"
        implementation "com.google.dagger:dagger-android-support:$dagger_version"
        kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    }

    hilt = dependencyGroup {
        // Hilt dependencies
        implementation "com.google.dagger:hilt-android:$hilt_version"
        kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

        // For Robolectric tests.
        testImplementation 'com.google.dagger:hilt-android-testing:2.28-alpha'
        kaptTest 'com.google.dagger:hilt-android-compiler:2.28-alpha'

        // For instrumented tests.
        androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
        kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    }

    commonLang = dependencyGroup {
        // Commons Lang - https://commons.apache.org/proper/commons-lang/
        implementation 'org.apache.commons:commons-lang3:3.11'
    }

    paging = dependencyGroup {
        // Paging 3 - https://developer.android.com/topic/libraries/architecture/paging/v3-overview
        def paging_version = "3.0.0-alpha12"
        implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    }

    gson = dependencyGroup {
        // Gson - https://github.com/google/gson
        implementation 'com.google.code.gson:gson:2.8.6'
    }

    okhttpLoggingInterceptor = dependencyGroup {
        // OkHttp - https://github.com/square/okhttp
        implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    }
}
